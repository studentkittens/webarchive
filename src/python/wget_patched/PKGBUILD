# $Id: PKGBUILD 109367 2011-02-09 03:01:13Z stephane $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=wget
pkgver=1.12
pkgrel=4
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'openssl' 'gettext' 'libidn')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        wget-1.12-CVE-2010-2252.patch
        wget-1.12-path.patch)
md5sums=('141461b9c04e454dc8933c9d1f2abf83'
         '2c8bc23eff98fd4efc3f96394fc8e61e'
         'fc4faf56d0ccb4a26ceb37294eadd7e6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Fix arbitrary file overwrite via 3xx redirect (CVE-2010-2252)
  patch -Np1 -i ../wget-1.12-CVE-2010-2252.patch

  # Remove references to /usr/local
  patch -Np1 -i ../wget-1.12-path.patch

  ./configure --prefix=/usr --sysconfdir=/etc --enable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
